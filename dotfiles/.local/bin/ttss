#! /usr/bin/env nu
source ~/.config/nushell/env.nu
let selected_dir = [code, composites, folios] | each {|e| reclist_directories $'($env.HOME)/synced/($e)' 2}
	| flatten
	| str join (char nl)
	| fzf

let selected_name = $selected_dir | split row "/" | last | str replace "." "_" --all;

# sets global interpreter, fails for local jupyter
let venv_dir = $"($selected_dir)/.venv"
let is_venv = ($venv_dir | path exists)

# https://github.com/amtoine/tmux-sessionizer at some point

def activate_venv [] {
	if $is_venv {
		tmux send-keys -t $selected_name $"source ($venv_dir)/bin/activate" C-m clear C-m
	}
}

let tfound = (tmux has-session -t $'=($selected_name)' | complete | get exit_code)

if ($tfound != 0) {
	let sid = $"($selected_name):"
	tmux new-session -ds $selected_name -c $selected_dir -n nvim
	activate_venv
	tmux new-window -t $sid -c "#{pane_current_path}"
	activate_venv
	tmux splitw -t $sid -h -l 40% -c "#{pane_current_path}"
	activate_venv
	tmux send-keys -t $sid nu C-m  
	tmux selectw -l -t $sid
	#tmux selectp -t 1
	#tmux splitw -v -p 75 -c "#{pane_current_path}"
	tmux send-keys -t $sid nvim C-m " sf" 
}

tmux attach -t $selected_name
